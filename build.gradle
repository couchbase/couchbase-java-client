/**
 * Copyright (C) 2014 Couchbase, Inc.
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in
 * all copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING
 * FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALING
 * IN THE SOFTWARE.
 */

import org.apache.tools.ant.filters.ReplaceTokens

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'maven-publish'

apply plugin: 'propdeps'
apply plugin: 'propdeps-maven'
apply plugin: 'propdeps-idea'
apply plugin: 'propdeps-eclipse'

apply plugin: 'checkstyle'

group = 'com.couchbase.client'
description = 'Official Couchbase Java Client Library'

buildscript {
    repositories {
        maven { url 'http://repo.spring.io/plugins-release' }
        jcenter()
    }
    dependencies {
        classpath 'org.springframework.build.gradle:propdeps-plugin:0.0.6'
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.0.3'
    }
}

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
}

checkstyle {
    toolVersion = "6.0"
}

def getVersionName() {
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'describe', '--always', '--dirty'
            standardOutput = stdout
        }
        return stdout.toString().trim()
    } catch (e) {
        //in case git command is not accessible...
        //put the date in there to have at least a reference point
        return "unknown" + new Date().format("yyyyMMdd");
    }
}

ext {
    coreVersion = '1.1.0-SNAPSHOT'

    slf4jVersion = '1.7.7'
    commonsVersion = '1.1.3'
    log4jVersion = '1.2.17'

    junitVersion = '4.11'
    mockitoVersion = '1.9.5'

    gitVersion = getVersionName()
}

sourceSets {
    integration
}

configurations {
    integrationCompile.extendsFrom compile, testCompile
    integrationRuntime.extendsFrom runtime, testRuntime

    markdownDoclet
}

dependencies {
    compile group: 'com.couchbase.client', name: 'core-io', version: coreVersion, changing: true

    optional group: 'org.slf4j', name: 'slf4j-api', version: slf4jVersion
    optional group: 'commons-logging', name: 'commons-logging', version: commonsVersion
    optional group: 'log4j', name: 'log4j', version: log4jVersion

    testCompile group: 'junit', name: 'junit', version: junitVersion
    testCompile group: 'org.mockito', name: 'mockito-all', version: mockitoVersion

    integrationCompile sourceSets.main.output

    markdownDoclet 'ch.raffael.pegdown-doclet:pegdown-doclet:1.1.1'
}

idea {
    module {
        scopes.PROVIDED.minus += configurations.compile
    }
}

tasks.withType(JavaCompile) {
    sourceCompatibility = "1.6"
    targetCompatibility = "1.6"
}

task integrationTest(type: Test) {
    testClassesDir = sourceSets.integration.output.classesDir
    classpath = sourceSets.integration.runtimeClasspath
}


task wrapper(type: Wrapper) {
    gradleVersion = '1.12'
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

javadoc {
    doLast {
        copy {
            from "docs"
            into "$buildDir/docs/javadoc"
        }
    }
}

javadoc.options {
    docletpath = configurations.markdownDoclet.files.asType(List)
    doclet = "ch.raffael.doclets.pegdown.PegdownDoclet"
    addStringOption("parse-timeout", "10")

    windowTitle = "Couchbase Java SDK"
    docTitle = "Couchbase Java SDK ($project.version)"
    bottom = "Copyright &copy; 2014 Couchbase, Inc."
    links = [
            "http://download.oracle.com/javase/6/docs/api/",
            "http://netty.io/4.0/api/",
            "http://fasterxml.github.io/jackson-databind/javadoc/2.4/",
            "http://reactivex.io/RxJava/javadoc/",
            "http://docs.couchbase.com/sdk-api/couchbase-core-io-" + coreVersion
    ]
    overview = "docs/teaser.html"
}


task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

def pomConfig = {
    name 'Couchbase Java SDK'
    url 'http://couchbase.com'
    organization {
        name 'Couchbase, Inc.'
        url 'http://couchbase.com'
    }
    licenses {
        license {
            name 'The Apache Software License, Version 2.0'
            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
            distribution 'repo'
        }
    }
    scm {
        url 'https://github.com/couchbase/couchbase-java-client'
        connection 'scm:git:git://github.com/couchbase/couchbase-java-client'
        developerConnection 'scm:git:git://github.com/couchbase/couchbase-java-client'
    }
    issueManagement {
        system "Couchbase JIRA"
        url "http://www.couchbase.com/issues/browse/JCBC"
    }
    developers {
        developer {
            id 'daschl'
            name 'Michael Nitschinger'
            email 'michael.nitschinger@couchbase.com'
        }
        developer {
            id 'simonbasle'
            name 'Simon BaslÃ©'
            email 'simon@couchbase.com'
        }
        developer {
            id 'avsej'
            name 'Sergey Avseyev'
            email 'sergey@couchbase.com'
        }
    }
}

publishing  {
    publications {
        compile(MavenPublication) {
            from components.java
            artifactId = 'java-client'

            artifact(javadocJar) {
                classifier = 'javadoc'
            }
            artifact(sourcesJar) {
                classifier = 'sources'
            }

            pom.withXml {
                def root = asNode()
                root.dependencies.'*'.findAll() {
                    it.scope*.value = 'compile'
                    if (it.artifactId.text() != 'core-io') {
                        it.appendNode('optional', 'true')
                    }
                }
                // For some weird reason description does not work in pomConfig, so doing it manually here.
                root.appendNode('description', 'The official Couchbase Java SDK');
                root.children().last() + pomConfig
            }
        }
    }

    repositories {
        maven {
            url "$buildDir/repo"
        }
    }
}

install {
    repositories.mavenInstaller {
        customizePom(pom)
    }
}

def customizePom(def pom) {
    pom.whenConfigured { generatedPom ->
        // eliminate test-scoped dependencies (no need in maven central poms)
        generatedPom.dependencies.removeAll { dep ->
            dep.scope == "test"
        }

        // sort to make pom dependencies order consistent to ease comparison of older poms
        generatedPom.dependencies = generatedPom.dependencies.sort { dep ->
            "$dep.scope:$dep.groupId:$dep.artifactId"
        }
    }
}

jar.manifest.mainAttributes(
        'Implementation-Title': 'Couchbase Java Client',
        'Implementation-Version': ext.gitVersion,
        'Implementation-Vendor': 'Couchbase, Inc.',
        'Specification-Title': 'com.couchbase.client.java',
        'Specification-Version': version,
        'Specification-Vendor': 'Couchbase, Inc.'
)

afterEvaluate {
    //resources in src/main/resources will have @tokens@ replaced
    configure(processResources) {
        filter(ReplaceTokens,
                tokens: [specVersion: version, implVersion: getVersionName()])
    }
}
